@App.module "AdvancedFiltersApp.Show", (Show, App, Backbone, Marionette, $, _) ->

  class Show.View extends Marionette.Layout
    template: 'advanced_filters/show/view'
    id: 'advanced-filters-show'

    regions:
      filterBarRegion: '#filter-bar-region'
      contestsRegion:  '#contests-region'
      locationsRegion: '#locations-region'

    events:
      'click #js-save-as-csv': (e) -> @saveAs(e, 'csv')
      'click #js-save-as-pdf': (e) -> @saveAs(e, 'pdf')
      'click #js-save-as-json': (e) -> @saveAs(e, 'json')
      'click #js-save-as-nist-xml': (e) -> @saveAs(e, 'nist')
      'click #js-save-as-vip-xml': (e) -> @saveAs(e, 'xml')
      'click #js-add-advanced-view-to-maps': (e) -> @addMap(e)
      'click #js-view-maps': (e) -> false
      'click #js-clear-maps': (e) -> @clearMaps(e)

    serializeData: ->
      data = {}
      data.filename = gon.data_filename
      data

    onShow: ->
      @filterBarRegion.show new Show.FilterBarView()
      @contestsRegion.show new Show.ContestsSelectorView()
      @locationsRegion.show new Show.LocationsSelectorView()
      $('[data-toggle="tooltip"]', @$el).tooltip();
      
      
    addMap: (e) ->
      e.preventDefault()
      af = App.request 'entities:advancedFilter'
      savedMaps = App.request('entities:savedMaps')
      data = af.requestData()
      url = "advanced-map/#{$.param(data)}"
      savedMaps.add_map(url)
      
      
    clearMaps: (e) ->
      e.preventDefault()
      savedMaps = App.request('entities:savedMaps')
      savedMaps.clearMaps()
      
    saveAs: (e, format) ->
      e.preventDefault()
      af = App.request 'entities:advancedFilter'
      data = af.requestData()
      data.lid = gon.locality_id
      url = gon.feed_url
      if format == 'nist'
        url = "#{url}-nist.xml"
      else
        url = "#{url}.#{format}"
      window.open "#{url}?#{$.param(data)}"
